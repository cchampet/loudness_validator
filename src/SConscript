Import( 'env' )
Import( 'buildMode' )
Import( 'installPath' )

# check libsndfile
conf = Configure(env)
if not conf.CheckLibWithHeader('sndfile', 'sndfile.h', 'c'):
    print 'Error: did not find sndfile library, exiting.'
    Exit(1)
env = conf.Finish()

# check boost accumulators (header only library) 
conf = Configure(env)
if not conf.CheckCXXHeader('boost/accumulators/accumulators.hpp'):
    print 'Error: did not find boost accumulators headers, exiting.'
    Exit(1)
env = conf.Finish()

# check swig application
if env.WhereIs('swig'):
    swigEnv = env.Clone()

    # According to swig, going to use optimisations turned on with gcc (for example -O2), ensure you do not break C/C++ aliasing rules
    if buildMode == 'release':
        swigEnv.Append( CXXFLAGS=[
            '-fno-strict-aliasing',
            ] )

    swigEnv.Append( SWIGFLAGS=[
      '-c++',
      '-java',
      '-package',
      'org.ploud',
      '-fcompact',
      '-small',
      '-O',
      '-Werror',
    ] )

    # loudnessAnalyser java shared library
    jloudnessAnalyserLib = swigEnv.SharedLibrary('jloudnessAnalyser', 'loudnessAnalyser.i')

    # Create jar
    classes = swigEnv.Java( target='classes', source='src', JAVAVERSION='1.7' )
    jar = swigEnv.Jar( target='jloudnessAnalyser.jar', source=['classes'], JARCHDIR='$SOURCE' )

    # Install
    swigEnv.Alias( 'install', swigEnv.InstallVersionedLib( installPath + '/lib/java', jloudnessAnalyserLib ) )
    swigEnv.Alias( 'install', swigEnv.InstallVersionedLib( installPath + '/share/java', jar ) )
else:
    print 'Swig not found: will not built java binding.'

# loudnessAnalyser shared library
loudnessAnalyserLib = env.SharedLibrary(
    'loudnessAnalyser',
    Glob( 'loudnessAnalyser/*.cpp' ) +
    Glob( 'loudnessAnalyser/utils/*.cpp' ),
)

# loudnessAnalyser static library
loudnessAnalyserLibStatic = env.StaticLibrary(
    'loudnessAnalyser',
    Glob( 'loudnessAnalyser/*.cpp' ) +
    Glob( 'loudnessAnalyser/utils/*.cpp' ),
)

# loudnessTools shared library
loudnessToolsLib = env.SharedLibrary(
    'loudnessTools',
    Glob( 'tool/*/*.cpp' ),
    LIBS = [
        "sndfile",
    ],
)

# loudnessTools static library
loudnessToolsLibStatic = env.StaticLibrary(
    'loudnessTools',
    Glob( 'tool/*/*.cpp' ),
    LIBS = [
        "sndfile",
    ],
)

env.Alias( 'install', env.InstallVersionedLib( installPath + '/lib', loudnessAnalyserLib ) )
env.Alias( 'install', env.Install( installPath + '/lib', loudnessAnalyserLibStatic ) )

env.Alias( 'install', env.InstallVersionedLib( installPath + '/lib', loudnessToolsLib ) )
env.Alias( 'install', env.Install( installPath + '/lib', loudnessToolsLibStatic ) )

env.Alias( 'install', env.Install( installPath + '/include/loudnessAnalyser', 'loudnessAnalyser/LoudnessAnalyser.hpp' ) )

Export( 'loudnessAnalyserLibStatic' )
Export( 'loudnessToolsLibStatic' )
