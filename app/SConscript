Import( 'env' )
Import( 'loudnessAnalyserLibStatic' )
Import( 'loudnessToolsLibStatic' )
Import( 'installPath' )

loudnessAnalyserProgram = env.Program(
	'loudness-analyser',
	Glob( 'analyser/*.cpp' ),
	LIBS = [
		loudnessAnalyserLibStatic,
		loudnessToolsLibStatic,
		"sndfile",
	]
)

loudnessCorrectorProgram = env.Program(
	'loudness-corrector',
	Glob( 'corrector/*.cpp' ),
	LIBS = [
		loudnessAnalyserLibStatic,
		loudnessToolsLibStatic,
		"sndfile",
	]
)

qtEnv = env.Clone()
qtEnv.Tool('qt')

loudnessValidatorLibraries = [
	loudnessAnalyserLibStatic,
	loudnessToolsLibStatic,
	"sndfile",
]

loudnessValidatorFrameworks = []

QtLibs = [
	"QtCore",
	"QtGui",
]

if not env['PLATFORM'] == "darwin":
	loudnessValidatorLibraries.append( QtLibs )
else:
	loudnessValidatorFrameworks.append( QtLibs )

loudnessValidatorProgram = qtEnv.Program(
	'loudness-validator',
	Glob( 'LoudnessValidator/*.cpp' ),
	LIBS = loudnessValidatorLibraries,
	FRAMEWORKS = loudnessValidatorFrameworks,
)

env.Alias( 'install', env.Install( installPath + '/bin', loudnessAnalyserProgram ) )
env.Alias( 'install', env.Install( installPath + '/bin', loudnessCorrectorProgram ) )
env.Alias( 'install', env.Install( installPath + '/bin', loudnessValidatorProgram ) )
